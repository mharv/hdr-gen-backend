#!/bin/csh
# Use this script to generate HDR images from multiple bracketed photo collections
# Input is a series of bracketed photo collections, each in a separate folder under 'originals'
# and camera response curve yourcamera.cam
# For each viewpoint, delivers exif data to exif/*.csv
# and unscaled HDR picture as pic/*.comb.hdr
# and tif image as tif/*.comb.tif

mkdir -p exif ; mkdir -p pic ; mkdir -p tif
rm tmp/*.hdr
rm tmp/*.pic

# takes one argument, the directory name holding the bracketed image set
set viewpoint = $1

# Canon use file suffix JPG not jpg - this fixes it and copies files to a temp folder.

echo $viewpoint
mkdir -p tmp/$viewpoint
rm tmp/$viewpoint/*.jpg
set i = 1
rm pic/$viewpoint.dat
echo -n Getting EXIF data $viewpoint ...
foreach image (`ls originals/$viewpoint | egrep 'JPG|jpg'`)
	cp originals/$viewpoint/$image tmp/$viewpoint/$i.jpg
	set pth = (originals/$viewpoint/$image)
	echo -n .
	exiftool -FileName $pth | awk '{print $4" "}' | tr -d  '"' | tr -d '\n' >> exif/$viewpoint.dat
	exiftool -FocalLength $pth | awk '{print $4" "}' | tr -d '\n' >> exif/$viewpoint.dat
	exiftool -CreateDate $pth | awk '{print $4" "$5" "}' | tr -d '\n' >> exif/$viewpoint.dat
	exiftool -ImageWidth $pth | awk '{print $4" "}' | tr -d '\n' >> exif/$viewpoint.dat
	exiftool -ImageHeight $pth | awk '{print $4" "}' | tr -d '\n' >> exif/$viewpoint.dat
	exiftool -ExposureTime $pth | awk '{print $4" "}' >> exif/$viewpoint.dat
	# ExposureTime doesn't port well into Excel when in format 1/x seconds
	@ i++
end
# DELIVERS exif data to exif/*.csv
# echo $viewpoint > exif/$viewpoint.csv
# echo Filename, Focal Length, CreationDate, CreationTime, PixelWidth,PixelHeight,ExposureTimeSeconds >> exif/$viewpoint.csv
cat exif/$viewpoint.dat | tr " " , >>  exif/$viewpoint.csv
rm exif/*.dat
echo DONE getting exif data.

#this combines the pictures in one single HDR image
#note also the yourcamera.cam is the profile of your camera, either you already have this or it was created by the previous step

rm pic/$viewpoint.comb.pic
echo Starting hdrgen for $viewpoint ...
hdrgen tmp/$viewpoint/*.jpg -r yourcamera.cam -o pic/$viewpoint.comb.pic
rm tmp/$viewpoint/*.jpg
echo DONE generating hdr.
	
# this displays a smaller version for quick inspection
# DELIVERS HDR combined images as tif's in the tif folder

echo Resizing image...
# pfilt -1 -x 4000 -y 2800 pic/$viewpoint.comb.pic > pic/$viewpoint.comb.hdr &
pfilt -1 -x 4000 -y 2800 pic/$viewpoint.comb.pic > pic/$viewpoint.comb.hdr 
# pfilt -x 1000 -p 1 pic/$viewpoint.comb.pic > tmp/$viewpoint.comb.hdr &
pfilt -x 1000 -p 1 pic/$viewpoint.comb.pic > tmp/$viewpoint.comb.hdr 
wait
echo Done resizing image.

echo Creating Tiff...
rm pic/*.pic
ra_tiff tmp/$viewpoint.comb.hdr tif/$viewpoint.comb.tif
echo Done creating Tiff...
rm -r tmp/$viewpoint
rm tmp/$viewpoint.comb.hdr

# the finished UNSCALED image is in the pic folder as *.comb.hdr
# the finished UNSCALED tif is in the tif folder as *.comb.tif
