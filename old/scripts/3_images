#!/bin/csh
# Use this script to post-process the scaled HDR images
# Input is scaled HDR images from previous script as pic/*.scaled.hdr
# Delivers image applying the human contrast sensitivity function as tif/*/*.s.tif
# and a series of falsecolour maps as tif/*/*.fc.*.tif
# and down-sized scaled HDR images as tmp/*.scaled.hdr
# and re-exposed scaled HDR image as tif/*/*.tif
# the last two can be re-exposed using scripts upexpose or downexpose


# take one argument, image name
set image = $1


#### establish a suitable exposure for the images
# open an empty file to store exposure data

# echo -n tmp/opti.txt
echo getting p values for $1 ...
# shrink each image to 20 x 14 pixels and then use pvalue  - this will give average luminance of each image. The get average of all viewpoint images.
# use this to set an optimum scale for all images


pfilt -1 -x 20 -y 14 pic/$image.scaled.hdr > tmp/$image.20x14.pic
pvalue -o -h -H tmp/$image.20x14.pic | rcalc -e '$1=$1;$2=$2;$3=($3*.265+$4*.67+$5*.065)*179' | awk '{printf "%.3f\n", $3}' | tr "\t" " " | total -m >>  tmp/opti.txt
rm tmp/$image.20x14.pic


# use this to set an optimum scale - uses 8 x average
set opti = `cat tmp/opti.txt | total -m | rcalc -e '$1=$1*8'`
rm tmp/opti.txt

# now sort out the exposure for each image
# default is +1
set exp = (+1)

echo p values retrieved.

echo creating false colour image for $image ...

# create image using human contrast sensitivity function
pcond -s pic/$image.scaled.hdr | ra_tiff -z -  tif/$image.s.tif &
wait



# establish a legend size for the falsecolour images, based on total image size
set dims = `getinfo -d pic/$image.scaled.hdr`
set lh = `echo $dims[3] | rcalc -e '$1=$1/4'` 
set lw = `echo $dims[3] | rcalc -e '$1=$1/8'` 
set fclw = `echo $dims[3] | rcalc -e '$1=($1/8)+4000'` # legend size for falsecolour - use this later when assembling arrays

# Create falsecolour maps

falsecolor -ip	pic/$image.scaled.hdr -s $opti	-lh $lh -lw $lw -n 10 -log 2 | ra_tiff - tif/$image.fc.optm.tif &
falsecolor -ip	pic/$image.scaled.hdr -s $opti	-lh $lh -lw $lw -n 10 -log 2 > tmp/$image.fc.optm.pic &
falsecolor -ip	pic/$image.scaled.hdr -s 1 		-lh $lh -lw $lw -n 10 -log 4 | ra_tiff - tif/$image.fc.1.tif &
falsecolor -ip	pic/$image.scaled.hdr -s 100 		-lh $lh -lw $lw -n 10 -log 4 | ra_tiff - tif/$image.fc.100.tif &
falsecolor -ip	pic/$image.scaled.hdr -s 10000 	-lh $lh -lw $lw -n 10 -log 4 | ra_tiff - tif/$image.fc.10000.tif &
# sleep 10 ; echo ; echo this may take a while...; echo
echo falsecolour generation complete.

# set image exposure and display preview
echo setting exposure for $1 ...
pfilt -x /5 -y /5 -e $exp pic/$image.scaled.hdr > tmp/$image.scaled.hdr
ra_tiff tmp/$image.scaled.hdr tif/$image.scaled.tif
pfilt -e $exp 	pic/$image.scaled.hdr | ra_tiff -z -  tif/$image.tif &
wait
echo processing complete.
